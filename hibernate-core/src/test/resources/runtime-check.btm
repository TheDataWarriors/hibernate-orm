HELPER org.hibernate.testing.junit4.runtimecheck.BMRuntimeCheckHelper

RULE mark session factory is being created (since Configuration)
CLASS org.hibernate.cfg.Configuration
METHOD buildSessionFactory
AT ENTRY
IF true
DO markSessionFactoryInCreation()
ENDRULE

RULE mark session factory has been created
CLASS org.hibernate.internal.SessionFactoryImpl
METHOD <init>
AT EXIT
IF true
DO markSessionFactoryCreated()
ENDRULE

RULE check regex pattern parsing is not used at runtime
CLASS Pattern
METHOD compile
IF shouldPerformAPICheck()
DO THROW new org.hibernate.session.runtime.check.CheckForbiddenAPIException("Regex pattern parsing");
ENDRULE

RULE check annotation is not read at runtime
INTERFACE ^AnnotatedElement
METHOD getAnnotation
IF annotationReadCheck()
DO THROW new org.hibernate.session.runtime.check.CheckForbiddenAPIException("Reading annotation");
ENDRULE

RULE check class is not loaded at runtime
CLASS Class
METHOD forName
IF classLookupCheck()
DO THROW new org.hibernate.session.runtime.check.CheckForbiddenAPIException("Loading class");
ENDRULE