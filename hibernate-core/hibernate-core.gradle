/*
 * Hibernate, Relational Persistence for Idiomatic Java
 *
 * License: GNU Lesser General Public License (LGPL), version 2.1 or later.
 * See the lgpl.txt file in the root directory or <http://www.gnu.org/licenses/lgpl-2.1.html>.
 */
import org.apache.tools.ant.filters.ReplaceTokens

description = 'Hibernate\'s core ORM functionality'

apply from: rootProject.file( 'gradle/published-java-module.gradle' )
apply plugin: 'antlr'
apply plugin: 'hibernate-matrix-testing'
apply plugin: 'org.hibernate.build.gradle.xjc'

ext {
    jaxbTargetDir = file( "${buildDir}/generated-src/jaxb/main" )
}

sourceSets.main {
    java.srcDir project.jaxbTargetDir
}

configurations {
    tests {
        description = 'Configuration for the produced test jar'
    }

    javassist {
        description "Dependencies for compiling and running the Javassist tests in the `javassist` source-set"
    }

//    //Configures the compile and runtime configurations for our javassist tests
//    //and includes the dependencies of the test task.
//    testJavassistCompile {
//        description "Dependencies for compiling the Javassist tests"
//        extendsFrom testCompile
//    }
//    testJavassistRuntime{
//        description "Dependencies for running the Javassist tests"
//        extendsFrom testRuntime
//    }
}

sourceSets {
    main {
        // add the XJC generated JAXB classes to the main source-set
        java.srcDir project.jaxbTargetDir
    }

    test {
        resources {
            // resources inherently exclude sources
            setSrcDirs( ['src/test/java','src/test/resources','src/test/bundles'] )
        }
    }

    testJavassist {
        // define the testJavassist source-set
        java {
            compileClasspath += main.output + test.output + configurations.javassist
            runtimeClasspath += main.output + test.output + configurations.javassist
        }
    }
}

dependencies {
    api libraries.jpa
    api libraries.jta
    api libraries.jandex
    api libraries.classmate

    implementation libraries.byteBuddy
    implementation libraries.commons_annotations
    implementation libraries.activation
    implementation libraries.jaxb_api
    implementation libraries.jaxb_runtime

    compileOnly libraries.jacc
    compileOnly libraries.validation
    compileOnly libraries.ant
    compileOnly libraries.cdi
    compileOnly configurations.javassist

    testImplementation project(':hibernate-testing')
    testImplementation libraries.shrinkwrap_api
    testImplementation libraries.shrinkwrap
    testImplementation libraries.jacc
    testImplementation libraries.validation
    testImplementation libraries.jandex
    testImplementation libraries.classmate
    testImplementation libraries.mockito
    testImplementation libraries.mockito_inline
    testImplementation libraries.jodaTime
    testImplementation libraries.assertj
    testImplementation libraries.log4j

    testImplementation libraries.cdi

    testImplementation( libraries.validator ) {
        // for test runtime
        transitive = true
    }

    // for testing stored procedure support
    testImplementation libraries.derby

    testRuntimeOnly( "org.jboss.spec.javax.ejb:jboss-ejb-api_3.2_spec:1.0.0.Final" )
    testRuntimeOnly( libraries.expression_language )
    testRuntimeOnly( 'jaxen:jaxen:1.1' )
    testRuntimeOnly( libraries.javassist )
    testRuntimeOnly( libraries.byteBuddy )
    testRuntimeOnly( libraries.weld )
    testRuntimeOnly( libraries.atomikos )
    testRuntimeOnly( libraries.atomikos_jta )
    testRuntimeOnly(libraries.wildfly_transaction_client)

    testAnnotationProcessor( project( ':hibernate-jpamodelgen' ) )

    testImplementation libraries.shrinkwrap_descriptors_api_javaee
    testImplementation libraries.shrinkwrap_descriptors_impl_javaee

    testImplementation libraries.jboss_ejb_spec_jar
    testImplementation libraries.jboss_annotation_spec_jar

    javassist libraries.javassist
    antlr libraries.antlr
    xjc libraries.jaxb_runtime
    xjc libraries.jaxb_xjc
    xjc libraries.jaxb2_basics
    xjc libraries.jaxb2_basics_ant
    xjc libraries.activation
}

jar {
    manifest {
        attributes(
                'Main-Class': 'org.hibernate.Version',

                // BND Plugin instructions (for OSGi):
                'Import-Package': [
                        'javax.security.auth;resolution:=optional',
                        // Make javax.security.jacc optional and do not reference a version range (because that's what we used to do)
                        'javax.security.jacc;resolution:=optional;version=!',
                        // Make javax.validation optional and do not reference a version range (because that's what we used to do)
                        'javax.validation;resolution:=optional;version=!',
                        'javax.validation.constraints;resolution:=optional;version=!',
                        'javax.validation.groups;resolution:=optional;version=!',
                        'javax.validation.metadata;resolution:=optional;version=!',
                        // Make javax.enterprise optional and do not reference a version range (because that's what we used to do)
                        '!javax.enterprise*',
                        'javax.enterprise.context.spi;resolution:=optional;version=!',
                        'javax.enterprise.inject.spi;resolution:=optional;version=!',
                        // For JPA, we don't want to target the automatically generated range, but a specific version
                        "javax.persistence;version=\"${project.jpaVersion.osgiName}\"",
                        // optionals
                        'jakarta.persistence.spi;resolution:=optional',
                        'javax.management;resolution:=optional',
                        'javax.naming.event;resolution:=optional',
                        'javax.naming.spi;resolution:=optional',
                        'org.apache.tools.ant;resolution:=optional',
                        'org.apache.tools.ant.taskdefs;resolution:=optional',
                        'org.apache.tools.ant.types;resolution:=optional',
                        'javax.inject;resolution:=optional',
                        'net.bytebuddy.*;resolution:=optional',
                        'org.objectweb.jonas_tm;resolution:=optional',
                        'com.ibm.websphere.jtaextensions;resolution:=optional',
                        // We must specify the version explicitly to allow Karaf
                        // to use an older version of JAXB (the only one we can use in Karaf)
                        "javax.xml.bind.*;version=\"${project.jaxbApiVersionOsgiRange}\"",
                        // Temporarily support JTA 1.1 -- Karaf and other frameworks still
                        // use it.  Without this, the plugin generates [1.2,2).
                        'javax.transaction;version="[1.1,2)"',
                        // Also import every package referenced in the code
                        '*'
                ].join( ',' ),
                '-exportcontents': [
                        // Legacy resource packages containing XSDs that were traditionally not exported
                        "!org.hibernate.xsd.cfg",
                        "!org.hibernate.xsd.mapping",
                        // TODO: Uncomment once EntityManagerFactoryBuilderImpl no longer uses ClassLoaderServiceImpl.
                        //'org.hibernate.boot.registry.classloading.internal',
                        "*;version=${project.version}"
                ].join( ',' ),
        )
    }
}

//idea {
//	module {
//		sourceDirs += file( "${buildDir}/generated-src/antlr/main" )
//	}
//}

xjc {
    outputDir = project.jaxbTargetDir

    schemas {
        cfg {
            xsd = file( 'src/main/resources/org/hibernate/xsd/cfg/legacy-configuration-4.0.xsd' )
            xjcBinding = file( 'src/main/xjb/hbm-configuration-bindings.xjb' )
        }
        hbm {
            xsd = file( 'src/main/resources/org/hibernate/xsd/mapping/legacy-mapping-4.0.xsd' )
            xjcBinding = file( 'src/main/xjb/hbm-mapping-bindings.xjb' )
            xjcExtensions = ['inheritance', 'simplify']
        }
        mapping {
            xsd = file( 'src/main/resources/org/hibernate/jpa/orm_2_2.xsd' )
            xjcBinding = file( 'src/main/xjb/mapping-bindings.xjb' )
            xjcExtensions = ['inheritance']
        }
    }
}

generateGrammarSource {
    arguments += "-traceParser"
}

//sourceSets.main.sourceGeneratorsTask.dependsOn xjc
//sourceSets.main.sourceGeneratorsTask.dependsOn generateGrammarSource
tasks.compile.dependsOn generateGrammarSource

task copyBundleResources (type: Copy) {
    ext {
        bundlesTargetDir = file( "${buildDir}/bundles" )
        bundleTokens = dbBundle[db]
        ext.bundleTokens['buildDirName'] = buildDir.absolutePath
    }

    from file('src/test/bundles/templates')
    into ext.bundlesTargetDir
    filter( ReplaceTokens, tokens: ext.bundleTokens)

    doFirst {
        ext.bundlesTargetDir.mkdirs()
    }
}
processTestResources.dependsOn copyBundleResources

task testJar(type: Jar, dependsOn: testClasses) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    archiveClassifier.set( 'test' )
    from sourceSets.test.output
}

artifacts {
    tests testJar
}

test {
    systemProperty 'file.encoding', 'utf-8'

    if ( gradle.ext.javaVersions.test.launcher.asInt() >= 9 ) {
        // See org.hibernate.boot.model.naming.NamingHelperTest.DefaultCharset.set
        jvmArgs( ['--add-opens', 'java.base/java.nio.charset=ALL-UNNAMED'] )
        // Weld needs this to generate proxies
        jvmArgs( ['--add-opens', 'java.base/java.security=ALL-UNNAMED'] )
        jvmArgs( ['--add-opens', 'java.base/java.lang=ALL-UNNAMED'] )
    }

    beforeTest { descriptor ->
        //println "Starting test: " + descriptor
    }
    // Allow to exclude specific tests
    if (project.hasProperty('excludeTests')) {
        filter {
            excludeTestsMatching project.property('excludeTests').toString()
        }
    }
}

//Create the task that runs the integration tests found from the
//configured source directory and uses the correct classpath.
task testJavassist(type: Test) {
    testClassesDirs = sourceSets.testJavassist.output.classesDirs
    classpath = sourceSets.testJavassist.runtimeClasspath
    //If you want to ensure that integration tests are run every time when you invoke
    //this task, uncomment the following line.
    //outputs.upToDateWhen { false }

    if ( gradle.ext.javaToolchainEnabled ) {
        // Configure version of Java tools
        javaLauncher = javaToolchains.launcherFor {
            languageVersion = gradle.ext.javaVersions.test.launcher
        }

        //  Configure JVM Options
        jvmArgs( getProperty( 'toolchain.launcher.jvmargs' ).toString().split( ' ' ) )

        // Display version of Java tools
        doFirst {
            logger.lifecycle "Testing javassist with '${javaLauncher.get().metadata.installationPath}'"
        }
    }

    if ( gradle.ext.javaVersions.test.launcher.asInt() >= 9 ) {
        // Javassist needs this to generate proxies
        jvmArgs( ['--add-opens', 'java.base/java.lang=ALL-UNNAMED'] )
    }
}

check.dependsOn testJavassist
testJavassist.mustRunAfter test
