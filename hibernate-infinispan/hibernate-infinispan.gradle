/*
 * Hibernate, Relational Persistence for Idiomatic Java
 *
 * License: GNU Lesser General Public License (LGPL), version 2.1 or later.
 * See the lgpl.txt file in the root directory or <http://www.gnu.org/licenses/lgpl-2.1.html>.
 */

mavenPom {
	name = '(deprecated - use org.infinispan:infinispan-hibernate-cache instead) Hibernate/Infinispan Integration'
	description = '(deprecated - use org.infinispan:infinispan-hibernate-cache instead) Integration for Infinispan into Hibernate as a second-level caching service'
}

def osgiDescription() {
	return mavenPom.description
}

jar {
	// The OSGi JAR manifest support does not like a non-existent classes dir,
	// so make sure we dont use the OSGi one :)
	manifest = null
}

sourcesJar {
	// The OSGi JAR manifest support does not like a non-existent classes dir,
	// so make sure we dont use the OSGi one :)
	manifest = null
}

def relocatedGroupId  = 'org.infinispan'
def relocatedArtifactId  = 'infinispan-hibernate-cache'
def relocatedVersion = '9.1.3.Final'

publishing {
	publications {
		relocationPom( MavenPublication ) {
			groupId 'org.hibernate'
			artifactId 'hibernate-infinispan'

			pom.withXml {
				def relocation = asNode().appendNode( 'distributionManagement' ).appendNode( 'relocation' )
				relocation.appendNode( 'groupId', relocatedGroupId)
				relocation.appendNode( 'artifactId', relocatedArtifactId )
				relocation.appendNode( 'version', relocatedVersion )
			}
		}
	}
}

