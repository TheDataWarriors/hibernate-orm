description = 'Maven plugin to integrate aspects of Hibernate into your build.'

//apply from: rootProject.file( 'gradle/published-java-module.gradle' )
apply from: rootProject.file( 'gradle/java-module.gradle' )

apply plugin: 'org.hibernate.build.maven-embedder'

/*
 * Hibernate, Relational Persistence for Idiomatic Java
 *
 * License: GNU Lesser General Public License (LGPL), version 2.1 or later.
 * See the lgpl.txt file in the root directory or http://www.gnu.org/licenses/lgpl-2.1.html.
 */

dependencies {
    implementation project( ":hibernate-core" )

    implementation "org.apache.maven:maven-plugin-api:3.6.3"
    implementation "org.apache.maven.plugin-tools:maven-plugin-annotations:3.6.0"
    implementation "org.apache.maven:maven-project:2.2.1"
    implementation "org.apache.maven.shared:file-management:3.1.0"
}

publishing {
    publications {
        hibernateMavenPlugin(MavenPublication) {
            from components.java
            pom.withXml {
                asNode()
                        .version
                        .plus {
                            packaging('maven-plugin')
                        }
                asNode()
                        .dependencies
                        .plus {
                            def plugins = build().appendNode('plugins')
                            def pluginPlugin = plugins.appendNode('plugin')
                            pluginPlugin.appendNode('groupId', 'org.apache.maven.plugins')
                            pluginPlugin.appendNode('artifactId', 'maven-plugin-plugin')
                            pluginPlugin.appendNode('version', '3.15.0')
                            def configuration = pluginPlugin.appendNode('configuration')
                            configuration.appendNode('goalPrefix', 'plugin')
                            configuration.appendNode('outputDirectory', 'target/generated/sources/plugin-descriptors/META-INF/maven' )
                        }
            }
        }
        hibernateCore(MavenPublication) {
            artifactId 'org.hibernate.orm'
            from project(":hibernate-core").components.java
        }
    }
    repositories {
        mavenLocal {
            url = layout.buildDirectory.dir('maven-embedder/maven-local')
        }
    }
}

tasks.generatePluginDescriptor.dependsOn generatePomFileForHibernateMavenPluginPublication
tasks.integrationTest.dependsOn publishHibernateCorePublicationToMavenLocalRepository

